USE Cli28Julho
GO
ALTER FUNCTION [dbo].[FC_ESPECIALIDADE_PESSOA_HORARIO]
(
 @ID_EMPRESA INT,
 @ID_ESPECIALIDADE INT,
 @DT_HORARIO DATE
)
RETURNS @TB_RETORNO TABLE
(
 ID_PESSOA INT,
 ID_OPT_DIASEMANA INT,
 ID_PROCEDIMENTO INT,
 NO_PESSOA VARCHAR(500),
 DS_INFORMACAO VARCHAR(500),
 HR_INICIO CHAR(5),
 HR_FIM CHAR(5),
 QT_ATENDIMENTO INT
)
AS
BEGIN
  INSERT INTO @TB_RETORNO (ID_PESSOA, ID_OPT_DIASEMANA, ID_PROCEDIMENTO, NO_PESSOA, QT_ATENDIMENTO, HR_INICIO, HR_FIM)
	SELECT PSESP.ID_PESSOA, PSHRR.ID_OPT_DIA_SEMANA, PSHRR.ID_PROCEDIMENTO, PESSO.NO_PESSOA,  PSHRR.QT_ATENDIMENTO, PSHRR.HR_INICIO, PSHRR.HR_FIM
	 FROM TB_PESSOA_ESPECIALIDADE PSESP
	  INNER JOIN TB_PESSOA_HORARIO PSHRR ON PSHRR.ID_PESSOA = PSESP.ID_PESSOA
		                                  AND (PSHRR.ID_EMPRESA = @ID_EMPRESA OR @ID_EMPRESA = 0)
																			AND PSHRR.ID_OPT_ATIVO = 715
		INNER JOIN TB_PESSOA PESSO ON PESSO.SQ_PESSOA = PSESP.ID_PESSOA
	 WHERE (PSESP.ID_EMPRESA = @ID_EMPRESA OR @ID_EMPRESA = 0)
	   AND PSESP.ID_ESPECIALIDADE = @ID_ESPECIALIDADE
	   AND PSHRR.DT_ESPECIAL = @DT_HORARIO

  INSERT INTO @TB_RETORNO (ID_PESSOA, ID_OPT_DIASEMANA, ID_PROCEDIMENTO, NO_PESSOA, QT_ATENDIMENTO, HR_INICIO, HR_FIM)
	SELECT PSESP.ID_PESSOA, PSHRR.ID_OPT_DIA_SEMANA, PSHRR.ID_PROCEDIMENTO, PESSO.NO_PESSOA,  PSHRR.QT_ATENDIMENTO, PSHRR.HR_INICIO, PSHRR.HR_FIM
	 FROM TB_PESSOA_ESPECIALIDADE PSESP
	  INNER JOIN TB_PESSOA_HORARIO PSHRR ON PSHRR.ID_PESSOA = PSESP.ID_PESSOA
		                                  AND (PSHRR.ID_EMPRESA = @ID_EMPRESA OR @ID_EMPRESA = 0)
		INNER JOIN TB_PESSOA PESSO ON PESSO.SQ_PESSOA = PSESP.ID_PESSOA
		INNER JOIN TB_OPCAO OPCAO ON OPCAO.SQ_OPCAO = PSHRR.ID_OPT_DIA_SEMANA
		 LEFT JOIN @TB_RETORNO RET ON RET.ID_PESSOA = PSESP.ID_PESSOA
		                          AND RET.ID_OPT_DIASEMANA = PSHRR.ID_OPT_DIA_SEMANA
	 WHERE (PSESP.ID_EMPRESA = @ID_EMPRESA OR @ID_EMPRESA = 0)
     AND PSESP.ID_ESPECIALIDADE = @ID_ESPECIALIDADE
	   AND PSHRR.DT_ESPECIAL IS NULL
		 AND PSHRR.ID_OPT_ATIVO = 715
		 AND OPCAO.CD_OPCAO = DATEPART(W, @DT_HORARIO)
		 AND RET.ID_PESSOA IS NULL

	UPDATE RET
	 SET DS_INFORMACAO = CONCAT(RET.NO_PESSOA, ' - ', ISNULL(PRC.NO_PROCEDIMENTO, '<Sem procedimento definido>'), ' [', CAST(RET.QT_ATENDIMENTO AS VARCHAR), '/', CAST(RET.QT_ATENDIMENTO - ISNULL(ISNULL(APQ.QT_AGENDAMENTO, AGQ.QT_AGENDAMENTO), 0) AS varchar)  , ']')
	 FROM @TB_RETORNO RET
	  LEFT JOIN TB_PROCEDIMENTO PRC ON PRC.SQ_PROCEDIMENTO = RET.ID_PROCEDIMENTO
		LEFT JOIN VW_AGENDAMENTO_QUANTIDADE AGQ ON AGQ.DT_AGENDAMENTO = @DT_HORARIO
		                                       AND AGQ.ID_PESSOA_PROFISSIONAL = RET.ID_PESSOA
		LEFT JOIN VW_AGENDAMENTO_PROCEDIMENTO_QUANTIDADE APQ ON APQ.DT_AGENDAMENTO = @DT_HORARIO
		                                                    AND APQ.ID_PESSOA_PROFISSIONAL = RET.ID_PESSOA
		                                                    AND APQ.ID_PROCEDIMENTO = RET.ID_PROCEDIMENTO 

  RETURN
END
GO
ALTER VIEW [dbo].[VW_PESSOA_ESPECIALIDADE]
AS
SELECT DISTINCT PSESP.ID_EMPRESA, PSESP.ID_ESPECIALIDADE, PSESP.ID_PESSOA, ESPEC.NO_ESPECIALIDADE, PESSO.NO_PESSOA, EMPRE.NO_PESSOA NO_EMPRESA, PSESP.DT_CONCLUSAO
  FROM TB_PESSOA_ESPECIALIDADE PSESP
   INNER JOIN TB_ESPECIALIDADE ESPEC ON ESPEC.SQ_ESPECIALIDADE = PSESP.ID_ESPECIALIDADE
	 INNER JOIN TB_PESSOA PESSO ON PESSO.SQ_PESSOA = PSESP.ID_PESSOA
	 INNER JOIN TB_PESSOA EMPRE ON EMPRE.SQ_PESSOA = PSESP.ID_EMPRESA
	 INNER JOIN TB_PESSOA_HORARIO PSHRR ON PSHRR.ID_PESSOA = PSESP.ID_PESSOA
	                                   AND PSHRR.ID_EMPRESA = PSESP.ID_EMPRESA
																		 AND PSHRR.ID_OPT_ATIVO = 715
																		 AND ISNULL(PSHRR.DT_ESPECIAL, DATEADD(DAY, 1, GETDATE())) >= GETDATE()
GO
CREATE VIEW VW_DIAS_SEMANA_PESSOA_HORARIO
AS
SELECT SQ_OPCAO ID_DIA_SEMANA, NO_OPCAO NO_DIA_SEMANA, CD_OPCAO CD_DIA_SEMANA, NO_OPCAO_ABREVIADO NO_DIA_SEMANA_ABREVIADO, IC_ATIVO FROM TB_OPCAO WHERE ID_TIPO_OPCAO = 114
UNION ALL
SELECT -1, 'Data Especial', 8 CD_DIA_SEMANA, 'DtE' NO_DIA_SEMANA_ABREVIADO, 'S'
GO
UPDATE TB_TIPO_OPCAO SET NO_TIPO_OPCAO = 'Status de Venda Clinica' WHERE SQ_TIPO_OPCAO = 119
GO
ALTER TABLE TB_PESSOA_HORARIO
 ALTER COLUMN ID_OPT_DIA_SEMANA INT NULL
GO
ALTER PROCEDURE [dbo].[SP_PESSOA_HORARIO_CAD]
(
 @SQ_PESSOA_HORARIO INT,
 @ID_EMPRESA INT,
 @ID_PESSOA INT,
 @ID_PROCEDIMENTO INT,
 @ID_OPT_DIA_SEMANA INT,
 @ID_OPT_ATIVO INT,
 @QT_ATENDIMENTO INT,
 @HR_INICIO CHAR(5),
 @HR_FIM CHAR(5),
 @DT_ESPECIAL DATETIME
)
AS
BEGIN
	IF ISNULL(@SQ_PESSOA_HORARIO, 0) = 0
		INSERT INTO TB_PESSOA_HORARIO
		(ID_EMPRESA,ID_PESSOA,ID_PROCEDIMENTO,ID_OPT_DIA_SEMANA,ID_OPT_ATIVO,
		 QT_ATENDIMENTO,HR_INICIO,HR_FIM,DT_ESPECIAL)
    VALUES
		(@ID_EMPRESA,@ID_PESSOA,@ID_PROCEDIMENTO,@ID_OPT_DIA_SEMANA,@ID_OPT_ATIVO,
		 @QT_ATENDIMENTO,@HR_INICIO,@HR_FIM,@DT_ESPECIAL)
	ELSE
		UPDATE TB_PESSOA_HORARIO
		 SET ID_EMPRESA = @ID_EMPRESA,
				 ID_PESSOA = @ID_PESSOA,
				 ID_PROCEDIMENTO = @ID_PROCEDIMENTO,
				 ID_OPT_DIA_SEMANA = @ID_OPT_DIA_SEMANA,
				 ID_OPT_ATIVO = @ID_OPT_ATIVO,
				 QT_ATENDIMENTO = @QT_ATENDIMENTO,
				 HR_INICIO = @HR_INICIO,
				 HR_FIM = @HR_FIM,
				 DT_ESPECIAL = @DT_ESPECIAL
		 WHERE SQ_PESSOA_HORARIO = @SQ_PESSOA_HORARIO
END
GO
ALTER TABLE TB_CLINICA_VENDA
 DROP COLUMN DT_PREVISAO_FATURAMENTO